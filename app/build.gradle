buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        //noinspection GradleDynamicVersion
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    def secretFile = file("${rootDir}/secret/secret.gradle")
    if (secretFile.exists()) {
        apply from: secretFile.absolutePath
        signingConfigs {
            release {
                storeFile project.ext.storeFile
                storePassword project.ext.storePassword
                keyAlias project.ext.keyAlias
                keyPassword project.ext.keyPassword
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix '.dev'
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (secretFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'ic_launcher.png'
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    compile  'com.android.support:appcompat-v7:21.0.+'
    compile ('com.android.support:recyclerview-v7:21.0.0') { exclude module: 'support-v4' }
    compile  'com.android.support:cardview-v7:21.0.0'
    compile  'com.jakewharton:butterknife:6.0.0'
    compile ('com.squareup.retrofit:retrofit:1.8.0') { exclude module: 'gson' }
    compile ('com.squareup.okhttp:okhttp-urlconnection:2.0.0') { exclude module: 'okhttp' }
    compile  'com.squareup.okhttp:okhttp:2.0.0'
    compile  'io.reactivex:rxandroid:0.23.0'
    compile  'com.google.code.gson:gson:2.3'
    compile  'com.google.dagger:dagger:2.0-SNAPSHOT'
    apt      'com.google.dagger:dagger-compiler:2.0-SNAPSHOT'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile fileTree(dir: 'libs', include: ['*.jar'])
}
